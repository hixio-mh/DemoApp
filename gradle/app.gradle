apply plugin: "com.android.application"

apply from: "$rootDir/gradle/common.gradle"

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim().toInteger()
def gitCommitDescribe = "git describe --tags".execute().text.trim()

android {
    defaultConfig {
        versionCode gitCommitCount
        versionName gitCommitDescribe
    }
    signingConfigs {
        release {
            keyAlias SIGN_KEY_ALIAS
            keyPassword SIGN_KEY_PASSWORD
            storeFile file(SIGN_STORE_FILE)
            storePassword SIGN_STORE_PASSWORD
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles "$rootDir/gradle/proguard-rules.pro"
        }
        debug {
            signingConfig signingConfigs.release
            jniDebuggable true
            renderscriptDebuggable true
            crunchPngs false
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "../../../app_${variant.versionName}_${variant.versionCode}_${flavorName}_${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation project(commonModule)

    debugImplementation libs.leakCanary
}